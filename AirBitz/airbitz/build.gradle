import java.util.regex.Pattern

buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://download.crashlytics.com/maven' }
        maven {
            url 'https://raw.github.com/embarkmobile/zxing-android-minimal/mvn-repo/maven-repository/'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
    }
}
apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    maven {
        url 'https://raw.github.com/embarkmobile/zxing-android-minimal/mvn-repo/maven-repository/'
    }
}

task buildAirbitzCore(type:Exec) {
    workingDir '../../'
    commandLine './mkabc'
}

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.1'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 21
        versionCode 2100000000
        versionName "99.99.99"
    }

    productFlavors {
        dev {
            applicationId 'com.airbitz.dev'
        }
        develop {
            applicationId 'com.airbitz.develop'
        }
        testnet {
            applicationId 'com.airbitz.testnet'
        }
        prod {
            applicationId 'com.airbitz'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        release {
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                def versionCode = getVersionCode()
                variant.outputs.each { output ->
                    if(variant.buildType.name.equals('release')) {
                        output.outputFile = new File(output.outputFile.parent, "airbitz-prod-" + variant.buildType.name +"-" + versionCode + ".apk")
                    }
                }
            }
        }
    }

    sourceSets.main.jni.srcDirs = [] //disable automatic ndk-build
}

// Used for appending the application version code in the generated .apk
def getVersionCode() {
    def manifestFile = file("src/main/AndroidManifest.xml")
    def pattern = Pattern.compile("versionCode=\"(\\d+)\"")
    def manifestText = manifestFile.getText()
    println manifestText
    def matcher = pattern.matcher(manifestText)
    matcher.find()
    return matcher.group(1).replace(".", "-")
}

// specify signing properties on the command line
if (hasProperty('storeFile')) {
    println 'Generating a signed package.'
    android.signingConfigs.release.storeFile = file(storeFile)
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyAlias = keyAlias
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = null
}

dependencies {
    compile 'com.android.support:support-v4:21.0.+'
    compile 'com.google.android.gms:play-services:6.5.87'
    compile 'com.squareup.picasso:picasso:2.3.4'
    compile 'com.squareup.retrofit:retrofit:1.4.1'
    compile 'commons-io:commons-io:2.4'
    compile 'com.google.zxing:core:3.0.1'
    compile 'net.hockeyapp.android:HockeySDK:3.0.2'

}

